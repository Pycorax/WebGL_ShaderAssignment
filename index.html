<!doctype>

<html>
    <head>
        <meta charset="utf-8" />
        <!-- Script Includes -->
		<script src="scripts/glMatrix-0.9.5.min.js" type = "text/javascript"></script>
		<script src="scripts/extraMath.js" type = "text/javascript"></script>
        <script src="scripts/material.js" type = "text/javascript"></script>
        <script src="scripts/mesh.js" type = "text/javascript"></script>
        <script src="scripts/light.js" type = "text/javascript"></script>
		<script src="scripts/camera.js" type = "text/javascript"></script>
        <script src="scripts/webGL.js" type = "text/javascript"></script>
        <!-- Shaders -->
        <script id ="FragmentShader" type = "x-shader/x-fragment">
            // Specify the precision of floats
            precision mediump float;

            // Light struct
            struct Light
            {
				float power;
                int type;
                vec3 position;
                vec3 direction;
                float kAmbient;
                float kDiffuse;
                float kSpecular;
                float kConstant;
                float kLinear;
                float kQuadratic;
                float spotInnerAngle;
                float spotOuterAngle;
            };

            // Light Types
            const int LIGHT_POINT = 0;
            const int LIGHT_DIRECTIONAL = 1;
            const int LIGHT_SPOT = 2;

            // Camera Info
            uniform vec3 viewDirection;

            // Lights
            const int MAX_LIGHTS = 8;
            uniform Light light[MAX_LIGHTS];

            // Material
            uniform vec3 diffuseColor;
            uniform vec3 ambientColor;
            uniform vec3 specularColor;
            uniform float shininess;

            // Fragment Details
            varying vec2 texCoord;
            varying vec3 normal;
            varying vec3 worldPos;

            // Texture
            uniform sampler2D uSampler;

            // Function Prototypes
            float calcAttenuation(Light _light, vec3 dirToThisVertex);

            void main(void)
            {
				// Set initial value of the fragColour
				gl_FragColor = vec4(0.0);

                // Store the final colour here
                vec4 finalColor = vec4(0.0);

				// Normalize the viewDirection
				vec3 eyeDirection = normalize(-viewDirection);

				// Loop through all the lights
				for (int i = 0; i < MAX_LIGHTS; ++i)
				{
					// Calculate the light direction
	                vec3 lightDir;
	                if (light[i].type == LIGHT_DIRECTIONAL)
	                {
	                    lightDir = normalize(light[i].direction);
	                }
	                else
	                {
	                    lightDir = normalize(light[i].position - worldPos);
	                }

	                // Calculate the reflection vector
	                vec3 reflectDir = normalize((viewDirection.xyz + lightDir) * 0.5);

	                // Calculate the Ambient
	                vec4 ambient = vec4(light[i].kAmbient * ambientColor, 1.0);

	                // Calculate the Diffuse and Specular
	                vec4 diffuse = light[i].power * vec4(light[i].kDiffuse * diffuseColor * max(dot(normal, lightDir), 0.0), 1.0);
	                vec4 specular = light[i].power * vec4(light[i].kSpecular * specularColor * pow(max(dot(normal, reflectDir), 0.0), shininess), 1.0);
	                finalColor = diffuse + specular;

	                // Add all the values together
	                gl_FragColor += calcAttenuation(light[i], lightDir) * vec4(ambient + (finalColor * texture2D(uSampler, texCoord)));
				}
            }

            float calcAttenuation(Light _light, vec3 dirToThisVertex)
            {
                float attenuation = 0.0;

                if (_light.type == LIGHT_POINT)
                {
                    float dist = distance(_light.position, worldPos);
                    attenuation = 1.0 / (_light.kConstant + _light.kLinear * dist + _light.kQuadratic * dist * dist);
                }
                else if (_light.type == LIGHT_DIRECTIONAL)
                {
                    attenuation = 1.0;
                }
                else if (_light.type == LIGHT_SPOT)
                {
                    float cosAngle = dot(_light.direction, dirToThisVertex);
                    float cosInner = cos(radians(_light.spotInnerAngle));
                    float cosOuter = cos(radians(_light.spotOuterAngle));

                    if (cosInner > cosAngle && cosAngle > cosOuter)
                    {
                        attenuation = (cosAngle - cosOuter) / (cosInner - cosOuter);
                    }
                    else if (cosAngle >= cosInner)
                    {
                        attenuation = 1.0;
                    }
                    else
                    {
                        attenuation = 0.0;
                    }
                }

                return attenuation;
            }
       </script>

       <script id ="VertexShader" type = "x-shader/x-vertex">
            attribute vec3 vPos;
            attribute vec3 vNorm;
            attribute vec2 vTex;

            uniform mat4 uMMatrix;
            uniform mat4 uVMatrix;
            uniform mat4 uPMatrix;
			uniform mat4 uMVInverseTransposeMatrix;

            varying vec2 texCoord;
            varying vec3 normal;
            varying vec3 worldPos;

            void main(void)
            {
                gl_Position = uPMatrix * uVMatrix * uMMatrix * vec4(vPos, 1.0);

				// Send these data to the Fragment Shader
                texCoord = vTex;
                normal = (uMVInverseTransposeMatrix * vec4(vNorm, 1.0)).xyz;
                worldPos = (uMMatrix * vec4(vPos, 1.0)).xyz;
            }
       </script>

	   <script id ="BasicVertexShader" type = "x-shader/x-vertex">
            attribute vec3 vPos;

            uniform mat4 uMMatrix;
            uniform mat4 uVMatrix;
            uniform mat4 uPMatrix;

            void main(void)
            {
                gl_Position = uPMatrix * uVMatrix * uMMatrix * vec4(vPos, 1.0);
            }
       </script>

	   <script id ="BasicFragmentShader" type = "x-shader/x-fragment">
		   // Specify the precision of floats
		   precision mediump float;

		   void main(void)
		   {
			   // Set initial value of the fragColour
			   gl_FragColor = vec4(1.0);
		   }
	  </script>
    </head>

    <body onload="Setup()" onkeydown="InputKeyDown(event)" onkeyup="InputKeyUp(event)">
        <canvas id="glCanvas" width="800" height="600">
            Your browser doesn't support HTML5 Canvas!
        </canvas>
        <div id = "Log">
            <b>Log:</b>
        </div>


    </body>
</html>
